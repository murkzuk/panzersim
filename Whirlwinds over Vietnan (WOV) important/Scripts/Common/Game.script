//-------------------------------------------------------------------
//
//  This code is copyright 2001 by G5 Software.
//  Any unauthorized usage, either in part or in whole of this code
//  is strictly prohibited. Violators WILL be prosecuted to the
//  maximum extent allowed by law.
//
//-------------------------------------------------------------------

class CGame
  extends ISerializeable
{
  boolean m_EscapeMenuLoaded  = false;
  boolean m_GamePaused        = false;
  String  m_StartMission      = "CC1M2Mission";
  Array   m_ControlState;

  boolean m_ArraysInited        = false;

  boolean m_PlayMusicAfterPause = false;

  boolean m_MissionStarted    = true;
  float   m_SoundFxVolume     = 0.0;

  int     m_Sit               = 0;
  int     m_CockpitMode       = 0;

  int     m_ColorSheme        = 0;

  // Process input commands
  boolean m_bWireframeMode    = false;
  boolean m_bStatsVisible     = false;
  Array   m_SaveControlState  = [];
  boolean m_EditorMode        = false;

  String    m_LoadingMissionName   = "";
  String    m_ProgressMissionName  = "";
  Component m_LoadingMission       = null;

  // Camera settings

  boolean m_IsEscMenuActive  = false;

  final static int CAMERA_NONE             = 0;
  final static int CAMERA_PLAYER           = 3;
  final static int CAMERA_FREE_PLAYER      = 4;

  boolean m_FreeCamera         = false;

  int     m_ViewCamera         = CAMERA_NONE;
  int     m_PreviousViewCamera = CAMERA_NONE;

  // Loading settings

  int    m_StartMissionPhase = -1;
  String m_MissionToStart;
  String m_PlayerSpawnPoint;
  Array  m_ObjectsPool;

  // Timer settings

//  Array   m_GameSpeeds        = [ 1.0 / 16.0, 1.0/ 4.0, 1.0, 2.0, 4.0, 8.0, 16.0 ];
//  Array   m_GameMsgSpeeds     = [ L"x1/16", L"x1/4", CGameMessages::str_msg_NormalSpeed, L"x2", L"x4", L"x8", L"x16"];

  Array   m_GameSpeeds        = [ 1.0/ 4.0, 1.0, 2.0, 4.0, 8.0 ];
  Array   m_GameMsgSpeeds     = [ L"x1/4", CGameMessages::str_msg_NormalSpeed, L"x2", L"x4", L"x8"];

  Array   m_PhysicsIterations = [ 
//                                    CBasePhysicsController::Iterations * 1,
                                    CBasePhysicsController::Iterations * 1,
                                    CBasePhysicsController::Iterations * 1,  
                                    CBasePhysicsController::Iterations * 2,//2  
                                    CBasePhysicsController::Iterations * 3,//4,  
                                    CBasePhysicsController::Iterations * 4//8,  
//                                    CBasePhysicsController::Iterations * 5 //16
                                ];

  Array   m_GameIterations    = [
//                                    [ 0,  0    ], // disable split game time
                                    [ 0,  0    ], // disable split game time
                                    [ 0,  0    ], // disable split game time
                                    [ 0,  500  ],
                                    [ 0,  500  ],
                                    [ 0,  500  ]
//                                    [ 0,  500  ]
                                ];

  int     m_CurrentGameSpeed  = 1; // normal
  int     m_PrevGameSpeed     = 1; // normal
  int     m_NormalGameSpeed   = 1;

  // Construction
  void CGame()
  {
    // Initialize text render
    (new #TextRender()).LoadFontsTable(new CFontTable());

    // Init input filter
    SetInputFilter(
      [
        CTLCMD_MENU_ESCAPE,
        CTLCMD_SHOW_MISSION_MENU,
        CTLCMD_FREE_PLAYER_CAMERA,
        CTLCMD_OPENCONSOLE,
        CTLCMD_FASTQUITGAME,
        CTLCMD_WIREFRAME,
        CTLCMD_SHOW_STATISTICS,
        CTLCMD_PAUSE_GAME,
        CTLCMD_TIMER_INCREASE,
        CTLCMD_TIMER_DECREASE,
        CTLCMD_TIMER_NORMAL,
        CTLCMD_PLAY_MUSIC,
        CTLCMD_NEXT_TRACK,
        CTLCMD_PREV_TRACK,
        CTLCMD_MUSIC_VOLUME_UP,
        CTLCMD_MUSIC_VOLUME_DOWN,
        CTLCMD_FIRE_PRIMARY,
        CTLCMD_FIRE_SECONDARY,
        CTLCMD_FIRE_THIRD,
        CTLCMD_TOGGLE_MFD1
      ]);

    // Create main camera
    RegisterObject(SOID_MainCamera, new #Camera<CMainCamera>());
    ActivateCamera(SOID_MainCamera, true);

    // Create console
    RegisterObject(SOID_Console, new #Console<CConsole>());

    // Create render statistics
    RegisterObject(SOID_RenderStatistics, new #RenderStatistics());

    // Create audio player
    RegisterObject("AudioPlayer", new #AudioPlayer());

    // Create music controller
    RegisterObject("MusicController", new #MusicController2<CMusicController>());

    RegisterObject("VideoPlayer", new #VideoPlayer());

    //  Screen Capture
    Component ScreenCapture = new #ScreenCapture();
    RegisterObject("ScreenCapture", ScreenCapture);

    Component MoviesList = new #MovieControl<CMoviesList>();
    RegisterObject("MoviesList", MoviesList);

    Component Intro = new CIntroSettings();

    RegisterObject("Intro", Intro);

    // Init stats visibility
    UpdateStatisticsVisibleState();

    // Initialize instances
    CInstances::InitializeInstances();
    CPlanarShadows::InitializePlanarShadows();

    // Initialize shadows
    CShadows::InitializeShadows();

    CIntersections::InitializeIntersections();

    // Notify game settings
    (new #GameSettings()).OnGameStarted();

    // Initial color sheme
    Component GameSettings = new #GameSettings();

    m_ColorSheme = GameSettings.GetCockpitDevicesColor();
    if (m_ColorSheme >= (CCommonStrings::CockpitColorCombo.size()-1))
      m_ColorSheme = 0;
  }

  void InitializeArrays()
  {
    if (m_ArraysInited)
      return;
    m_ArraysInited = true;

    // Static initialization
    CEffectsArray::StaticInitialization();
    CSoundsArray::StaticInitialization();
  }

  void StartGame()
  {

    // create movies list
    Component MoviesList = GetObject("MoviesList");
    MoviesList.Setup(GetObject("Intro").Movies);
    MoviesList.SetOnEndEvent(SOID_GameController, "OnIntroPlayFinished");

    // play movies
    MoviesList.PlayList();

//    GetMenuController().EnableMenuMusic(false);

  }

  event void OnIntroPlayFinished()
  {
    // Создать главное меню
    CreateMainMenu();

  }

  void Shutdown()
  {
    UnloadMission();
    DeleteMenuController();
  }

  event void BeginNewGame()
  {
    UnloadMission();
    DeleteMenuController();
    StartMission(m_StartMission);
    m_EscapeMenuLoaded = false;
  }

  void SetupPlayer()
  {
    Component Mission = GetLoadedMission();
    if (null != Mission)
    {
      Mission.CreatePlayerObject();

      // Activate object
      ActivateObject("MainPlayerUnit", true, true);
    }
  }

  void SetEditorMode(
      boolean _InEditor
    )
  {
    m_EditorMode = _InEditor;
  }

  void SwitchPause()
  {
    m_GamePaused = !m_GamePaused;    

    if (m_GamePaused)
    {
      m_PlayMusicAfterPause = GetObject("MusicController").IsPlaying();
      GetObject("MusicController").Pause();
                              
    }
    else
    {
      if (m_PlayMusicAfterPause)
        GetObject("MusicController").ContinueOrPlay();
    }

    PauseGame(m_GamePaused);
    fireEvent(0.0, [], "OnGamePaused", [m_GamePaused]);
  }

  boolean IsPaused()
  {
    return m_GamePaused;
  }

  void SaveCS()
  {
    m_ControlState = GetControlState();
  }

  void RestoreCS()
  {
    SetControlState(m_ControlState);
    m_ControlState.clear();
  }

  event void BeginMission()
  {
    if (m_MissionStarted)
      return;

    Component GameController = new #GameController();
    if (GameController.IsPaused())
      GameController.SwitchPause();

    Component MenuController = GetMenuController();
    MenuController.LoadNewMenu("LoadingScreenMenu");
    Component Menu = MenuController.GetActiveMenu();
    Menu.SetProgress(1002);
    RenderIntermediateFrame();

    m_MissionStarted = true;
    logMessage("Mission started");

    // Hide loading screen
    HideLoadingScreen();
    Component GameSettings = new #GameSettings();
//    GameSettings.SetSoundFxVolume(m_SoundFxVolume);
    Component MainPlayerUnit = GetLoadedMission().GetObject("MainPlayerUnit");

    MainPlayerUnit.SetImmortalMode(GameSettings.GetGodMode());
    MainPlayerUnit.SetUnlimitedFuel(GameSettings.GetUnlimitedFuel());

    CEndMissionMenu::BLDKilled = 0;
    CEndMissionMenu::VEHKilled = 0;
    CEndMissionMenu::HitsCount = 0;
    CEndMissionMenu::ShootsCount = 0;

    (new #InputController()).ResetState();
    m_EscapeMenuLoaded = false;
  }

  void OnInputCommand(
      int _UserCommand
    )
  {
    if (!m_MissionStarted)
      BeginMission();

    Component Mission = GetLoadedMission();
    if (null != Mission)
    {
      if (Mission.IsSinemaPlayed())
      {
        sendEvent(0.0f, SOID_MissionController, "StopCameraScript", []);
        return;
      }
    }

    if (CTLCMD_SHOW_MISSION_MENU == _UserCommand)
    {

      if (!IsPaused())
        SwitchPause();

      if (IsPaused())
      {
        SaveCS();

        Component MenuController = GetMenuController();

        if (false == m_EscapeMenuLoaded)
        {
          MenuController.LoadNewMenu("EscapeMenu");
          m_EscapeMenuLoaded != m_EscapeMenuLoaded;
        }
        MenuController.GetObject("Cursor").ShowCursor(true);
        MenuController.LockChangeInputMode(false);
        EnableControl("GameMenu", true);
      }
    }
    else
    if (CTLCMD_FREE_PLAYER_CAMERA == _UserCommand)
    {
      m_FreeCamera = !m_FreeCamera;

      if (m_FreeCamera)
      {
        m_CockpitMode = (new #GameSettings()).GetCockpitMode();

        (new #GameSettings()).SetCockpitMode(CM_Person3rd);
        fireEvent(0.0, [], "OnCockpitModeChanged", [CM_Person3rd]);

        Component Settings = new #GameSettings();
        Settings.SetCockpitDevicesColor(CCommonStrings::CockpitColorCombo.size() - 1);
        fireEvent(0.0, [], "OnCockpitColorChanged", [Settings.GetCockpitDevicesColor()]);

        m_Sit         = GetLoadedMission().GetObject("MainPlayerUnit").GetPlayerControlSit();
      }
      else
      {
        (new #GameSettings()).SetCockpitMode(m_CockpitMode);
        fireEvent(0.0, [], "OnCockpitModeChanged", [m_CockpitMode]);

        Component Settings = new #GameSettings();
        Settings.SetCockpitDevicesColor(m_ColorSheme);
        fireEvent(0.0, [], "OnCockpitColorChanged", [Settings.GetCockpitDevicesColor()]);

        GetLoadedMission().GetObject("MainPlayerUnit").SetPlayerControlSit(m_Sit);
      }

      GetLoadedMission().SwitchToFreeCamera(m_FreeCamera);
    }
    else
    if (CTLCMD_OPENCONSOLE == _UserCommand)
    {
      Component Console = GetObject(SOID_Console);
      if (Console != null)
        Console.Enable(true);
    }
    else
    if (CTLCMD_FASTQUITGAME == _UserCommand)
    {
      m_SaveControlState = [];
      QuitGame();
    }
    else
    if (CTLCMD_WIREFRAME == _UserCommand)
    {
      m_bWireframeMode = !m_bWireframeMode;
      if (m_bWireframeMode)
        SetWireframeRenderMode();
      else
        SetSolidRenderMode();
    }
    else
    if (CTLCMD_SHOW_STATISTICS == _UserCommand)
    {
      m_bStatsVisible = !m_bStatsVisible;
      UpdateStatisticsVisibleState();
    }
    else
    if (CTLCMD_PAUSE_GAME == _UserCommand)
    {
      SwitchPause();
    }
    else 
    if (CTLCMD_TIMER_INCREASE == _UserCommand)
    {
      m_CurrentGameSpeed++;
      if (m_CurrentGameSpeed >= m_GameSpeeds.size())
        m_CurrentGameSpeed = m_GameSpeeds.size() - 1;
        
      setGameTimerSpeed(m_GameSpeeds[m_CurrentGameSpeed]);
      SetUpdateIterations(m_GameIterations[m_CurrentGameSpeed][0], m_GameIterations[m_CurrentGameSpeed][1]);

      // Set to mission
      Component Mission = GetLoadedMission();
      if (null != Mission)
        Mission.SetPhysicsIterations(m_PhysicsIterations[m_CurrentGameSpeed]);
      fireEvent(0.0, [], "OnGameSpeedChanged", []);
    }
    else
    if (CTLCMD_TIMER_DECREASE == _UserCommand)
    {
      m_CurrentGameSpeed--;
      if (m_CurrentGameSpeed < 0)
        m_CurrentGameSpeed = 0;

      setGameTimerSpeed(m_GameSpeeds[m_CurrentGameSpeed]);
      SetUpdateIterations(m_GameIterations[m_CurrentGameSpeed][0], m_GameIterations[m_CurrentGameSpeed][1]);      

      // Set to mission
      Component Mission = GetLoadedMission();
      if (null != Mission)
        Mission.SetPhysicsIterations(m_PhysicsIterations[m_CurrentGameSpeed]);
      fireEvent(0.0, [], "OnGameSpeedChanged", []);
    }
    else
    if (CTLCMD_TIMER_NORMAL == _UserCommand)
    {
      m_CurrentGameSpeed = m_NormalGameSpeed;
      setGameTimerSpeed(m_GameSpeeds[m_CurrentGameSpeed]);
      SetUpdateIterations(m_GameIterations[m_CurrentGameSpeed][0], m_GameIterations[m_CurrentGameSpeed][1]);      

      // Set to mission
      Component Mission = GetLoadedMission();
      if (null != Mission)
        Mission.SetPhysicsIterations(m_PhysicsIterations[m_CurrentGameSpeed]);

      fireEvent(0.0, [], "OnGameSpeedChanged", []);
    }
    else
    if (CTLCMD_PLAY_MUSIC == _UserCommand)
    {
      Component MusicController = GetObject("MusicController");
      boolean IsPlaying = MusicController.IsPlaying();
      if (IsPlaying)
      {
        MusicController.Pause();
      }      
      else
      {
        MusicController.ContinueOrPlay();
        Component GameSettings = new #GameSettings();  
        if (GameSettings.GetMusicVolume() < 0.1f)
        {
          fireEvent(0.0, [], "ShowMusicVolume", [GameSettings.GetMusicVolume()]);          
          return;
        }
         
      }
      fireEvent(0.0, [], "OnMusicPlay", [!IsPlaying]);
    }
    else
    if (CTLCMD_NEXT_TRACK == _UserCommand)
    {
      Component MusicController = GetObject("MusicController");
      MusicController.Play(MusicController.GetCurrentMusic() + 1);
      fireEvent(0.0, [], "ShowMusicTrackNumber", []);
    }
    else
    if (CTLCMD_PREV_TRACK == _UserCommand)
    {
      Component MusicController = GetObject("MusicController");
      if (MusicController.GetCurrentMusic() != -1)
        MusicController.Play(MusicController.GetCurrentMusic() - 1);

      fireEvent(0.0, [], "ShowMusicTrackNumber", []);
    }
    else
    if (CTLCMD_MUSIC_VOLUME_DOWN == _UserCommand)
    {
      Component GameSettings = new #GameSettings();  
      GameSettings.SetMusicVolume(GameSettings.GetMusicVolume() - 0.1f);
      fireEvent(0.0, [], "ShowMusicVolume", [GameSettings.GetMusicVolume()]);
    }
    else
    if (CTLCMD_MUSIC_VOLUME_UP == _UserCommand)
    {
      Component GameSettings = new #GameSettings();  
      GameSettings.SetMusicVolume(GameSettings.GetMusicVolume() + 0.1f);
      fireEvent(0.0, [], "ShowMusicVolume", [GameSettings.GetMusicVolume()]);
    }
    else
      logWarning("Unsupported command in game script: " + new String(_UserCommand));
  }

  event void QuitToMainMenu()
  {
    // Unload current level
    UnloadMission();

    // Reset menus
    DeleteMenuController();
    
    // Show main menu
    CreateMainMenu();

    ActivateCamera(SOID_MainCamera, true);
  }

  event void QuitMission()
  {
    UnloadMission();

    // Reset menus
    DeleteMenuController();
    
    // Show main menu
    CreateMainMenu();

    ActivateCamera(SOID_MainCamera, true);
  }

  // ==================================
  // Camera
  // ==================================

  void SetViewCamera(
      int _ViewCamera
    )
  {
  // $TMP cameras later

    if (m_IsEscMenuActive)
      return;

    Component Mission = GetMission();
    if (Mission == null)
      return;

    boolean bSuccess = true;

    if (CAMERA_PLAYER == _ViewCamera)
    {
      Mission.SwitchToPlayerCamera();
    }
    else
    if (CAMERA_FREE_PLAYER == _ViewCamera)
    {
      Mission.SwitchToFreePlayerCamera();
    }
    else
    {
      LogWarning("[Game] Invalid View Camera requested");
    }

    if (bSuccess)
    {
      m_PreviousViewCamera = m_ViewCamera;
      m_ViewCamera         = _ViewCamera;
    }
  }

  void SetPreviousViewCamera()
  {
    SetViewCamera(m_PreviousViewCamera);
  }

  void ResetViewCamera()
  {
     m_ViewCamera         = CAMERA_PLAYER;
     m_PreviousViewCamera = CAMERA_PLAYER;     
  }

  boolean IsFreeCameraMode()
  {
    return m_FreeCamera;
  }

  // Rendering control
  void SetWireframeRenderMode()
  {
//    SetLayerFillMode(PRIMLAYER_ATMOSPHERE,        FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_TERRAIN,           FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_TERRAIN_PATCH,     FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_ROADS,             FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_FX_UNDER_WATER,    FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_WATER,             FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_SCENE,             FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_FOREST,            FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_SHADOW,            FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_EFFECTS,           FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_TRANSPARENT_SCENE, FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_TRANSPARENT_NEAR,  FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_NEAR,              FILLMODE_WIREFRAME);
    SetLayerFillMode(PRIMLAYER_GRASS,             FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_FENCE,             FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_LIGHT_MAPS,        FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_FLARES,            FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_COCKPIT,           FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_MENU,              FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_MENU_DIALOG,       FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_STATISTICS,        FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_CURSOR,            FILLMODE_WIREFRAME);
//    SetLayerFillMode(PRIMLAYER_CONSOLE,           FILLMODE_WIREFRAME);
  }

  void SetSolidRenderMode()
  {
//    SetLayerFillMode(PRIMLAYER_ATMOSPHERE,        FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_TERRAIN,           FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_TERRAIN_PATCH,     FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_ROADS,             FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_FX_UNDER_WATER,    FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_WATER,             FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_SCENE,             FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_FOREST,            FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_SHADOW,            FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_EFFECTS,           FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_TRANSPARENT_SCENE, FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_TRANSPARENT_NEAR,  FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_NEAR,              FILLMODE_SOLID);
    SetLayerFillMode(PRIMLAYER_GRASS,             FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_FENCE,             FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_LIGHT_MAPS,        FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_FLARES,            FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_COCKPIT,           FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_MENU,              FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_MENU_DIALOG,       FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_STATISTICS,        FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_CURSOR,            FILLMODE_SOLID);
//    SetLayerFillMode(PRIMLAYER_CONSOLE,           FILLMODE_SOLID);
  }

  void UpdateStatisticsVisibleState()
  {
    Component RenderStats = GetObject(SOID_RenderStatistics);
    if (RenderStats != null)
      RenderStats.Enable(m_bStatsVisible);

    Component GameServer = GetObject(SOID_GameServer);
    if (GameServer != null)
      GameServer.ShowStatistics(m_bStatsVisible);

    Component GameClient = GetObject(SOID_GameClient);
    if (GameClient != null)
      GameClient.ShowStatistics(m_bStatsVisible);
  }

  // ======================================
  // Menu methods
  // ======================================

  Component GetMenuController()
  {
    return GetObject("GameMenu");
  }

  Component CreateMenuController()
  {
    Component Menu = GetMenuController();
    if (null != Menu)
      return Menu;

    Menu = new #MenuController<CMenuController>();
    RegisterObject("GameMenu", Menu);

    return Menu;
  }

  void DeleteMenuController()
  {
    Component Menu = UnregisterObject("GameMenu");
    if (Menu != null)
    {
      shutdownComponent(Menu);
      Menu = null;
    }
  }

  void CreateMainMenu()
  {
    // Create main menu
    CreateMenuController();
    Component MenuController = GetMenuController();
    MenuController.LoadNewMenu("MainMenu");
    MenuController.LockChangeInputMode(true);
    EnableControl("GameMenu", true);
  }

  // ======================================
  // Mission methods
  // ======================================

  Component GetLoadedMission()
  {
    if (null != m_LoadingMission)
      return m_LoadingMission;
    else
      return GetObject(SOID_MissionController);
  }

  void LoadMission(
      String _MissionName
    )
  {
    LoadMission(_MissionName, true);
  }

  void LoadMission(
      String  _MissionName,
      boolean _CreateObjects
    )
  {
    m_LoadingMissionName = _MissionName;

    // Show loading screen
    ShowLoadingScreen(_MissionName, true);

    // Create mission component
    Component Mission = CreateMission(_MissionName);
    if (Mission == null)
      return;

    // Setup physics itertaions
    Mission.SetPhysicsIterations(m_PhysicsIterations[m_CurrentGameSpeed]);

    // Hide mission
    setVisibleState(Mission, false);

    // First initialization of arrays
    InitializeArrays();

    // Construct mission
    float fTime = GetGameTime();
    Mission.Construct(_CreateObjects);

    // Register mission and initialize it
    RegisterObject(SOID_MissionController, Mission);
    m_LoadingMission = null;

    // Initialize mission
    Mission.Initialize();

    // Print message about loding time
    logMessage("Mission loading duration: " + new String(GetGameTime() - fTime));

    // Show mission
    setVisibleState(Mission, true);

    UpdateWaitScreen();

    // Render current view
//    RenderIntermediateFrame();
  }

  void UpdateWaitScreen()
  {
    Component MenuController = GetMenuController();
    MenuController.LoadNewMenu("LoadingScreenMenu");
    Component Menu = MenuController.GetActiveMenu();
    Menu.SetProgress(1000);

//    MenuController.GetObject("Cursor").ShowCursor(false);
//    MenuController.LockChangeInputMode(false);
    EnableControl("GameMenu", true);
    ActivateCamera(SOID_MainCamera, true);

    RenderIntermediateFrame();
  }

  void UnloadMission()
  {
    // Check mission was loaded
    if (null == GetLoadedMission())
      return;

    clearEventsQueue();

    // Unregister mission object
    Component Mission = UnregisterObject(SOID_MissionController);

    // Delete mission object
    if (Mission != null)
    {
      shutdownComponent(Mission);
      Mission = null;
      m_LoadingMissionName = "";
    }
    SetControlState([]);
  }

  Component CreateMission(
      String _MissionName
    )
  {
    // Create mission component
    m_LoadingMission = new #MissionController();
    setIdentificator(m_LoadingMission, SOID_MissionController);

    // Load it mission from script
    if (!loadFromScript(m_LoadingMission, _MissionName))
      return null;

    // Return loaded mission
    return m_LoadingMission;
  }

  Component GetMission()
  {
    return GetObject(SOID_MissionController);
  }

  String GetLoadingMissionName()
  {
    return m_LoadingMissionName;
  }

  String GetProgressMissionName()
  {
    return m_ProgressMissionName;
  }

  Component m_CurrentObjectProps;

  void SetCurrentObjectProps(Array _Properties)
  {
    m_CurrentObjectProps = new CPropertiesMap(_Properties);
  }

  void ClearCurrentObjectProps()
  {
    m_CurrentObjectProps = null;
  }

  Component GetCurrentObjectProps()
  {
    return m_CurrentObjectProps;
  }

  // ======================================
  // Loading Methods
  // ======================================

  event void StartMission(
      String _MissionName
    )
  {
    StartMission(_MissionName, "MainPlayerUnitInitPlace");
  }

  event void StartMission(
      String _MissionName,
      String _PlayerSpawnPoint
    )
  {
    m_StartMissionPhase  = 1;  //$TMP
    m_MissionToStart     = _MissionName;
    m_PlayerSpawnPoint   = _PlayerSpawnPoint;

    sendEvent(0, SOID_GameController, "ContinueStartingMission", []);
    m_EscapeMenuLoaded = false;
  }

  event void ContinueStartingMission()
  {
    if (0 == m_StartMissionPhase)
    {
      sendEvent(0, SOID_GameController, "ContinueStartingMission", []);
      m_StartMissionPhase++;
    }
    else
    if (1 == m_StartMissionPhase)
    {
      UnloadMission();

      sendEvent(0, SOID_GameController, "ContinueStartingMission", []);

      m_StartMissionPhase++;
    }
    else
    if (2 == m_StartMissionPhase)
    {
      EnableControl("GameMenu", true);

      // Setup new mission
      LoadMission(m_MissionToStart);

      UpdateWaitScreen();

      Component GameController = new #GameController();
      if (!GameController.IsPaused())
        GameController.SwitchPause();

      Component GameSettings = new #GameSettings();
//      m_SoundFxVolume = GameSettings.GetSoundFxVolume();
//      GameSettings.SetSoundFxVolume(0.0);

      sendEvent(0, SOID_GameController, "ContinueStartingMission", []);

      // Play starting cinematics of new mission
//      if (!RunCinema(GetLoadedMission().CINEMA_Start))
//        sendEvent(0, SOID_GameController, "ContinueStartingMission", []);

      m_StartMissionPhase++;
    }
    else
    if (3 == m_StartMissionPhase)
    {
      UpdateWaitScreen();
   
      SetupPlayer();

      UpdateWaitScreen();

      GetLoadedMission().StartMission();

      UpdateWaitScreen();

      sendEvent(0, SOID_GameController, "ContinueStartingMission", []);
      m_StartMissionPhase++;
    }
    else
    if (4 == m_StartMissionPhase)
    {

      logMessage("Mission loaded");
      m_MissionStarted = false;

      UpdateWaitScreen();

      Component MenuController = GetMenuController();
      MenuController.LoadNewMenu("LoadingScreenMenu");
      Component Menu = MenuController.GetActiveMenu();

      Menu.SetProgress(1001);

      m_StartMissionPhase++;
      m_EscapeMenuLoaded = false;
    }
  }

  boolean m_IsLoadingScreenVisible = false;

  void ShowLoadingScreen(
      String  _MissionName,
      boolean _UpdateScreen
    )
  {
    if (m_IsLoadingScreenVisible)
      return;

    m_IsLoadingScreenVisible = true;
    m_ProgressMissionName    = _MissionName;

    // Show loading screen
    Component MenuController = CreateMenuController();
    MenuController.LoadNewMenu("LoadingScreenMenu");
    MenuController.GetObject("Cursor").ShowCursor(false);
    MenuController.LockChangeInputMode(true);
    EnableControl("GameMenu", true);
    ActivateCamera(SOID_MainCamera, true);

    UpdateLoadProgress(0, 100, 0);
    if (_UpdateScreen)
      RenderIntermediateFrame();
  }

  void HideLoadingScreen()
  {
    if (!m_IsLoadingScreenVisible)
      return;
    m_IsLoadingScreenVisible = false;

    EnableControl("GameMenu", false);
    ActivateCamera(SOID_MainCamera, false);
    Component MenuController = CreateMenuController();
    MenuController.GetObject("Cursor").ShowCursor(true);
    MenuController.GoBack();

    MenuController.BaseMenu_FreeUnusedMaterials();
  }

  void UpdateLoadProgress(
      int _Num,
      int _Total,
      int _Part
    )
  {
    Component MenuController = GetMenuController();
    if (null == MenuController)
      return;

    Component Menu = GetMenuController().GetActiveMenu();
    if (null == Menu)
      return;

    // Compute progress by part
    int progress = 0;

    if (0 == _Part)
      progress =       _Num * 900 / _Total;
    else
    if (1 == _Part)
      progress = 900 + _Num * 100 / _Total;

    // Setup progress
    Menu.SetProgress(progress);

    // Update bar omn screen
    RenderIntermediateFrame();
  }

  // ======================================
  // Cinema methods
  // ======================================

  Array GetCinemaList()
  {
    Array Result = [];

    Component Mission = GetLoadedMission();
    if (null != Mission)
    {
      Component MissionContent = Mission.GetContent();
      if (null != MissionContent)
        return MissionContent.GetCinemaList();
    }

    return [];
  }

  boolean RunCinema(
      String _CinemaID
    )
  {
    return RunCinema(_CinemaID, "");
  }

  boolean RunCinema(
      String _CinemaID,
      String _ObjectToActivate
    )
  {
    m_PrevGameSpeed = m_CurrentGameSpeed;
    m_CurrentGameSpeed = m_NormalGameSpeed;
    setGameTimerSpeed(m_GameSpeeds[m_CurrentGameSpeed]);
    SetUpdateIterations(m_GameIterations[m_CurrentGameSpeed][0], m_GameIterations[m_CurrentGameSpeed][1]);

    // Set to mission
    Component Mission = GetLoadedMission();
    if (null != Mission)
      Mission.SetPhysicsIterations(m_PhysicsIterations[m_CurrentGameSpeed]);
    fireEvent(0.0, [], "OnGameSpeedChanged", []);

    sendEvent(0.0f, SOID_MissionController, "PlayCameraScript", [_CinemaID, 0.0, _ObjectToActivate]);
    return false;
  }

  event void OnCinemaEnd()
  {    
    m_CurrentGameSpeed = m_PrevGameSpeed;
    setGameTimerSpeed(m_GameSpeeds[m_CurrentGameSpeed]);
    SetUpdateIterations(m_GameIterations[m_CurrentGameSpeed][0], m_GameIterations[m_CurrentGameSpeed][1]);

    // Set to mission
    Component Mission = GetLoadedMission();
    if (null != Mission)
    {
      Mission.SetPhysicsIterations(m_PhysicsIterations[m_CurrentGameSpeed]);
      if (Mission.IsSinemaPlayed())
        sendEvent(0.0f, SOID_MissionController, "StopCameraScript", []);
    }
    fireEvent(0.0, [], "OnGameSpeedChanged", []);

    ActivateObject("ScriptedCamera", false, false);
    ActivateObject("FreeCamera", m_FreeCamera, false);

  }


  // ======================================
  // ISerializeable
  // ======================================

  void Import(
      Component _Storage
    )
  {
    if (null != GetLoadedMission())
      UnloadMission();

    String MissionName = _Storage.ImportValue();

    logMessage("name = " + MissionName);

    LoadMission(MissionName);
    Component Mission = GetLoadedMission();

    if (null != Mission)
    {
      _Storage.ImportObject(Mission);
      Mission.Import(_Storage);
    }
    else
      logError("no mission");
  }

  void Export(
      Component _Storage
    )
  {
    Component Mission = GetLoadedMission();

    _Storage.ExportValue(m_LoadingMissionName);

    if (null != Mission)
    {
      _Storage.ExportObject("MissionController", Mission);
      Mission.Export(_Storage);
    }
    else
      logError("no mission");
  }

  int m_DifficultyLevelLatch = -1;

  int GetDifficultyLevel()
  {
    return (new #GameSettings()).GetDifficultyLevel();
  }

  void SetDifficultyLevel(int _Level)
  {
    if (m_DifficultyLevelLatch == -1)
    {
      m_DifficultyLevelLatch = GetDifficultyLevel();
    }

    (new #GameSettings()).SetDifficultyLevel(_Level);
  }

  void ResetDifficultyLevel()
  {
    if (m_DifficultyLevelLatch != -1)
    {
      (new #GameSettings()).SetDifficultyLevel(m_DifficultyLevelLatch);
      m_DifficultyLevelLatch = -1;
    }
  }

  //
  // Mutli-Player
  //

    String GetGameMode()
  {
//    if (GetObject(SOID_GameClient) != null)
//      return "Client";

//    if (GetObject(SOID_GameServer) != null)
//      return "Server";

//    return "Single";

    //TMP
    return "Server";
  }

  Component GetGameSession()
  {
//    Component Client = GetObject(SOID_GameClient);
//    if (Client != null)
//      return Client;

//    Component Server = GetObject(SOID_GameServer);
//    if (Server != null)
//      return Server;

    // TMP
    Component Server = GetObject(SOID_GameServer);
    return Server;
  }

  WString GetCurrentGameSpeedMsg()
  {
    return m_GameMsgSpeeds[m_CurrentGameSpeed];
  }  

  boolean IsNormalGameSpeed()
  {
    return m_CurrentGameSpeed == m_NormalGameSpeed;
  }

}
